plugins {
    id 'java-library'
}


repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    coroutines {
        transitive = false
    }
}

dependencies {
    coroutines project(":xar-core")

    coroutines project(":xar-converters:jackson")
    coroutines project(":xar-converters:fastjson")
    coroutines project(":xar-converters:gson")
}

task sourcesJar(type: Jar) {
    def module_projects = configurations.coroutines.dependencies.collect {
        it.getDependencyProject()
    }
    module_projects.each {
        from it.sourceSets.main.allSource
    }
    archiveClassifier.set('sources')
}


jar {

    dependsOn configurations.coroutines

    from {
        configurations.coroutines.findAll { true }.collect { zipTree(it) }
    }

    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
}


publishing {

    publications {
        mavenJava(MavenPublication) {
            groupId "$project.group"
            artifactId "$project.name"
            version "$project.version"

            from components.java

            artifact sourcesJar

            pom {
                name = "$project.name"
                description = 'XAR-JSON '
                url = 'https://github.com/lawyerance/xar-json/**/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lawyerance'
                        name = 'lawyerance'
                        email = 'lawyerance@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
